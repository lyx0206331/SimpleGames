apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "com.adrian.games"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        // dex突破65535的限制
        multiDexEnabled true
        // AndroidManifest.xml 里面UMENG_CHANNEL的value为 ${UMENG_CHANNEL_VALUE}
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "ClassicGames"]
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['src/main/resources']
            aidl.srcDirs = ['src/main/aidl']
            renderscript.srcDirs = ['src/maom']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
            jniLibs.srcDir 'src/main/jniLibs'
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    //执行lint检查，有任何的错误或者警告提示，都会终止构建，我们可以将其关掉。
    lintOptions {
        abortOnError false
    }

    //签名
    signingConfigs {
        debug {
//            storeFile file("C:/Users/卿/.android/debug.keystore")  //windows
            storeFile file("/Users/ranqing/.android/debug.keystore")    //mac
//            storeFile file("/home/adrian/.android/debug.keystore")    //ubuntu

        }
        release {
            //这样写就得把签名文件放在项目目录
//            storeFile file("D:/workspace/appkey.keystore")
//            storeFile file("/Users/ranqing/Documents/android-sdk-mac_android_studio/appkey.keystore")
            storeFile file("/home/adrian/Android/appkey.keystore")
            storePassword "lengyuxuan1534."
            keyAlias "adrian_keystore"
            keyPassword "lengyuxuan1534."
        }
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            //签名
            signingConfig signingConfigs.release
            //混淆
            minifyEnabled true
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //渠道Flavors，配置不同风格的app
    productFlavors {
        "default" {}
//        GooglePlay {}
//        google {}
//        "91" {}
//        hiapk {}
//        gfan {}
//        goapk {}
//        appChina {}
//        mumayi {}
//        eoe {}
//        nduo {}
//        feiliu {}
//        crossmo {}
//        huawei {}
//        QQ {}
//        "3G" {}
//        "360" {}
//        baidu {}
//        sohu {}
//        "163" {}
//        UC {}
//        dangle {}
//        samsung {}
//        mmw {}
//        xiaomi {}
//        lenovo {}
//        nearme {}
//        wandoujia {}
    }
    //批量配置
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = "SimpleGame_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile files('libs/umeng-analytics-game-v5.6.4.0.jar')
    compile 'com.jakewharton:butterknife:8.4.0'
    compile 'de.cketti.library.changelog:ckchangelog:1.2.2'
}
